finish
/clear
/title, fixed - fixed concrete beam example
/prep7
et,1,65
mp,ex,1,3e7       ! steel rebar (units are pounds, inches)
mp,ex,2,1e6,      ! concrete
mp,dens,2,.00025
tb,concr,2
tbdata,1,.3,.5,200,4000        ! shear coeffs, tensile and compress strength
r,1,1,.03,0,0     ! mat 1 (steel), 3 percent reinforcement in x dir
r,2,1,.01,0,0     ! mat 1 (steel), 1 percent 
r,3,1,.04,0,0     ! mat 1 (steel), 4 percent
block,,100,,5,,5
block,,100,5,10,,5
block,,100,10,15,,5
!vovlap,all
NUMMRG,KP, , , ,LOW 
numcmp,volu
esize,5
mat,2        ! concrete material 
real,1       ! rebar 
vmesh,1
real,2
vmesh,2
real,3
vmesh,3
nsel,s,loc,x
d,all,all
nsel,s,loc,x,100
d,all,all
nsel,all
fini
/solu
nsel,s,loc,y,15
sf,all,pres,100
nsel,all
OUTRES,ALL,ALL, 
nsub,10
solve
fini
/post1
/DEVICE,VECTOR,1
set,last
plcrack
fini