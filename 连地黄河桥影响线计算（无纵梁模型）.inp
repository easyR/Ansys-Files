fini
/clear
!连地黄河大桥计算模型(3x80 影响线)
/config,NRES,100000
/PREP7  
!* 
ET,1,LINK1  

R,1,0.03389, ,   !不同杆件截面积
R,2,0.02672, ,
R,3,0.02114, ,
R,4,0.03224, ,
R,5,0.03198, ,
R,6,0.0154, ,
R,7,0.01976, ,
R,8,0.02499, ,
R,9,0.02157, ,
R,10,0.03526, ,
R,11,0.02344, ,
R,12,0.0106, ,

MPTEMP,1,0  
MPDATA,EX,1,,2.1e8
MPDATA,PRXY,1,,0.3 
TYPE,1  
MAT,1
REAL,2
ll=3*80              !梁全长
hh=11                 !桁梁高
ww=8                  !节间长
nn1=ll/ww+1           !一侧主桁下弦纵向节点最大编号
!
!以下完成单侧平面桁架单元的建立
n,1,0,0,0              !下弦第一点
n,nn1,ll,0,0            !下弦最后一点
fill,1,nn1
n,nn1+1,ww,hh,0           !上弦第一点
n,2*nn1-2,ll-ww,hh,0           !上弦最后一点
fill,nn1+1,2*nn1-2
*DO,ii,1,nn1-1,1          !下弦单元
  e,ii,ii+1
*enddo
*do,ii,nn1+1,2*nn1-3,1      !上弦单元
  e,ii,ii+1
*enddo
  e,1,nn1+1                 !第一端斜杆
*do,ii,3,nn1-2,2            !奇数点
  e,ii,nn1+ii-2
  e,ii,nn1+ii-1
  e,ii,nn1+ii
*enddo
  e,nn1,2*nn1-2             !最后一根端斜杆
*do,ii,2,nn1-1,2            !偶数点，只有竖杆
  e,ii,nn1+ii-1
*enddo

!选择单元
esel,s,,,1,2
esel,a,,,9,12
esel,a,,,19,22
esel,a,,,29,30
!改变实常数（截面积）
emodif,all,real,1
esel,all
esel,s,,,15,16
esel,a,,,37,38
esel,a,,,41,42
esel,a,,,47,48
esel,a,,,51,52
emodif,all,real,3

esel,all
esel,s,,,33,36
esel,a,,,39,40
esel,a,,,49,50
esel,a,,,53,56
emodif,all,real,4

esel,all
emodif,59,real,5
emodif,74,real,5
emodif,87,real,5
emodif,102,real,5
emodif,60,real,6
emodif,63,real,6
emodif,65,real,6
emodif,96,real,6
emodif,98,real,6
emodif,101,real,6
emodif,62,real,7
emodif,66,real,7
emodif,77,real,7
emodif,80,real,7
emodif,81,real,7
emodif,84,real,7
emodif,95,real,7
emodif,99,real,7
emodif,68,real,8
emodif,69,real,8
emodif,78,real,8
emodif,83,real,8
emodif,92,real,8
emodif,93,real,8
emodif,71,real,9
emodif,75,real,9
emodif,86,real,9
emodif,90,real,9
emodif,72,real,10
emodif,89,real,10

esel,all
esel,s,,,61,100,3
emodif,all,real,11

esel,all
esel,s,,,103,117
emodif,all,real,12
esel,all

/solu
!D,1,UX                    !约束，可根据实际情况增加约束
D,1,UY 
D,11,UY
D,21,UY
D,21,ux
D,nn1,UY
!施加荷载
!加载

x0=0                           !从-1.0开始加载
x1=240
LHY=0                  !加载平面的竖向坐标
D_l=8                         !单位荷载移动步长
nHzb=nint((x1-x0)/D_l+0.5)+1
*DIM,hzwz,ARRAY,nHzb          !定义荷载位置数组

xx=x0                         !单位荷载移动位置

*do,ii,1,nHzb,1
!  TIME,(ii)*Delt_Time          !结束时间
  KBC,0                       !台阶加载
  NSUBST,5                    !子步数
  fdele,all,all
      *if,xx,gt,x1,then
        *exit
      *endif    
  nsel,s,node,,node(xx,LHY,0) 
  esln
  *get,emin,elem,,num,min
  ee=emin        
  edy=0                     !先给荷载单元赋值 
  ecount=elmiqr(0,13)      
  *do,nn,1,ecount
      ye1= ny(nelem(ee,1))    !单元节点坐标  
      ye2= ny(nelem(ee,2))
      xe1= nx(nelem(ee,1))
      xe2= nx(nelem(ee,2))
      *if,abs(ye1-ye2),le,1e-6,and,(xx-xe1)*(xx-xe2),le,0,then
          edy=ee
      *endif
      ee=elnext(ee)                            
  *enddo
      cc=abs(nx(nelem(edy,1))-nx(nelem(edy,2)))          !单元x方向长
      aa=abs(nx(nelem(edy,1))-xx)                       !I节点距荷载长度
      bb=abs(nx(nelem(edy,2))-xx)                       !J节点距荷载长度
      hzi=1*bb/cc                           !I节点等效荷载
      hzj=1*aa/cc                           !j节点等效荷载

      allsel,all,all
      f,nelem(edy,1),fy,-hzi
      f,nelem(edy,2),fy,-hzj
  
  hzwz(ii)=xx                    !将荷载的实际位置存入数组
  solve
  xx=xx+D_l 
*enddo

*CFOPEN,hzwz,txt
*do,kk,1,ii,1
zzz=hzwz(kk)
*cfwrite,, zzz
*enddo
*cfclos

/post26
numvar,200
nnum=2
nsol,nnum,6,u,y,6uy
nsol,nnum+1,16,u,y,16uy
nsol,nnum+2,26,u,y,26uy
