fini
/clear
!长东黄河桥96m桁梁计算(2019年桥梁检测计算模型)
/config,NRES,100000
/PREP7  
!* 
ET,1,LINK1  

!不同杆件截面积

R,1,0.033744
R,2,0.03704 
R,3,0.038688
R,4,0.01444
R,5,0.01772
R,6,0.0106

MPTEMP,1,0  
MPDATA,EX,1,,2.1e8
MPDATA,PRXY,1,,0.3 
MPDATA,DENS,1,,7.8         !定义材料密度，和加速度配合

TYPE,1  
MAT,1
REAL,4
ll=1*96               !梁全长
hh=16                 !桁梁高
ww=12                  !节间长
nn1=ll/ww+1           !一侧主桁下弦纵向节点最大编号
!ACEL,,10             !施加y方向重力加速度，本计算不施加
!
!以下完成单侧平面桁架单元的建立（X 桁架）
n,1,0,0,0              !下弦第一点
n,nn1,ll,0,0            !下弦最后一点
fill,1,nn1
nn2=nn1+1             !上弦第一点编号
n,nn2,ww,hh,0         
n,nn2+nn1-3,ll-ww,hh,0           !上弦最后一点
fill,nn2,nn2+nn1-3
nn3=nn2+nn1-2            !中间节点第一点编号
n,nn3,ww/2,hh/2,0
n,nn3+nn1-2,ll-ww/2,hh/2,0
fill,nn3,nn3+nn1-2

*DO,ii,1,nn1-1,1          !下弦单元
  e,ii,ii+1
*enddo
*do,ii,nn2,nn2+nn1-4,1      !上弦单元
  e,ii,ii+1
*enddo

*do,ii,1,nn1-2,1            !向右倾斜腹杆
  e,ii,nn3+ii-1
  e,nn3+ii-1,nn2+ii-1
*enddo

*do,ii,3,nn1,1            !向左倾斜腹杆
  e,ii,nn3+ii-2
  e,nn3+ii-2,nn2+ii-3
*enddo
e,2,nn3                   !第一根向左倾斜腹杆
e,nn1-1,nn3+nn1-2         !最后一根向右倾斜腹杆

!以下增加一些额外的杆件（专属于96m桁梁）

e,2,10
e,8,16


!选择单元
esel,s,,,3,6
esel,a,,,9
esel,a,,,14
!改变实常数（截面积）
emodif,all,real,1

esel,s,,,2
esel,a,,,7
esel,a,,,10,13
esel,a,,,15,16
esel,a,,,41,42
esel,a,,,19,20
esel,a,,,37,38
emodif,all,real,2

esel,all
esel,s,,,1
esel,a,,,8
emodif,all,real,3

esel,all
esel,s,,,17,18
esel,a,,,39,40
emodif,all,real,5

esel,all

/solu
 !约束，可根据实际情况增加约束
D,1,UY,0
D,9,UY,0
D,9,UX,0

!单位荷载加载命令流
/solu

x0=0                           !从0开始加载
x1=96
LHY=0                   !加载平面的竖向坐标
D_l=12                         !单位荷载移动步长
nHzb=nint((x1-x0)/D_l+0.5)+1
*DIM,hzwz,ARRAY,nHzb          !定义荷载位置数组

xx=x0                         !单位荷载移动位置

*do,ii,1,nHzb,1
!  TIME,(ii)*Delt_Time          !结束时间
  KBC,0                       !台阶加载
  NSUBST,5                    !子步数
  fdele,all,all
      *if,xx,gt,x1,then
        *exit
      *endif    
  nsel,s,node,,node(xx,LHY,0) 
  esln
  *get,emin,elem,,num,min
  ee=emin        
  edy=0                     !先给荷载单元赋值 
  ecount=elmiqr(0,13)      
  *do,nn,1,ecount
      ye1= ny(nelem(ee,1))    !单元节点坐标  
      ye2= ny(nelem(ee,2))
      xe1= nx(nelem(ee,1))
      xe2= nx(nelem(ee,2))
      *if,abs(ye1-ye2),le,1e-6,and,(xx-xe1)*(xx-xe2),le,0,then
          edy=ee
      *endif
      ee=elnext(ee)                            
  *enddo
      cc=abs(nx(nelem(edy,1))-nx(nelem(edy,2)))          !单元x方向长
      aa=abs(nx(nelem(edy,1))-xx)                       !I节点距荷载长度
      bb=abs(nx(nelem(edy,2))-xx)                       !J节点距荷载长度
      hzi=1*bb/cc                           !I节点等效荷载
      hzj=1*aa/cc                           !j节点等效荷载

      allsel,all,all
      f,nelem(edy,1),fy,-hzi
      f,nelem(edy,2),fy,-hzj
  
  hzwz(ii)=xx                    !将荷载的实际位置存入数组
  solve
  xx=xx+D_l 
*enddo

*CFOPEN,hzwz,txt
*do,kk,1,ii,1
zzz=hzwz(kk)
*cfwrite,, zzz
*enddo
*cfclos

!取得影响线表
 /POST26 
ESOL,2,1,1,smisc,1,E0E2 
ESOL,3,2,2,smisc,1,E2E4
ESOL,4,4,4,smisc,1,E6E8

ESOL,5,9,10,smisc,1,A2A3
ESOL,6,11,12,smisc,1,A6A7

ESOL,7,15,1,smisc,1,E0C1
ESOL,8,43,2,smisc,1,E2C1
ESOL,9,45,2,smisc,1,E2A2    
ESOL,10,17,2,smisc,1,E2C3
ESOL,11,19,3,smisc,1,E4C5
 
NSOL,12,5,u,y,E8_y 
NSOL,13,1,u,x,E0_x  


