fini
/clear
!计算桁架影响线
/PREP7  
!* 
ET,1,LINK1  
ET,2,BEAM3  
R,1,1, ,
R,2,1,1,1, , , ,  
MPTEMP,1,0  
MPDATA,EX,1,,2.1e11 
MPDATA,PRXY,1,,0.3 
TYPE,1  
MAT,1
REAL,1
ll=1*75              !梁全长
hh=10                 !桁梁高
ww=7.5                  !节间长
nn1=ll/ww+1           !一侧主桁下弦纵向节点最大编号
!
!以下完成单侧平面桁架单元的建立
n,1,0,0,0              !下弦第一点
n,nn1,ll,0,0            !下弦最后一点
fill,1,nn1
n,nn1+1,ww,hh,0           !上弦第一点
n,2*nn1-2,ll-ww,hh,0           !上弦最后一点
fill,nn1+1,2*nn1-2
n,2*nn1-1,ww/2,hh/2,0           !再分杆第一点
n,3*nn1-3,ll-ww/2,hh/2,0           !再分杆最后一点
fill,2*nn1-1,3*nn1-3
*DO,ii,1,nn1-1,1          !下弦单元
  e,ii,ii+1
*enddo
*do,ii,nn1+1,2*nn1-3,1      !上弦单元
  e,ii,ii+1
*enddo


TYPE,2   
MAT,1
REAL,2   
!建立纵梁模型
k,1,0,0,0                   !第一点
*Do,ii,1,nn1-1,1                        
  k,ii+1,ii*ww,0,0
  l,ii,ii+1
*ENDDO

D_l=0.1               !网格单元长度0.1
lesize,all,D_l     !划分纵梁单元网格
lmesh,all

NUMMRG,NODE, , , ,LOW     !合并节点  
NUMCMP,NODE               !压缩节点编号

/solu
D,1,UX                    !约束，可根据实际情况增加约束
D,1,UY 
D,nn1,UY
!施加荷载

jz_x1=0                       !加载位置起点x坐标（必须为节点）
jz_x2=75                      !加载位置终点x坐标
nHzb=(jz_x2-jz_x1)/D_l+1      !  荷载步
!单元荷载
E_Q=1

!施加荷载
Delt_Time=D_l      !通过每单元的时间
*do,ii,1,nHzb,1
  TIME,ii*Delt_Time          !结束时间
  KBC,0                       !台阶加载
  NSUBST,5                    !子步数
  fdele,all,all
  x_l=(ii-1)*D_l         !第一个荷载位置相对于加载起点位置
  f,NODE(x_l,0,0),FY,-E_Q
  solve  
*enddo
fini



