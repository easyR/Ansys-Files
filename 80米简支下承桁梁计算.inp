fini
/clear
!80米下承式钢桁梁模型(影响线)
/config,NRES,100000
/PREP7  
!* 
ET,1,LINK1  


R,1,1     !不同杆件截面积

MPTEMP,1,0  
MPDATA,EX,1,,2.1e8
MPDATA,PRXY,1,,0.3 
TYPE,1  
MAT,1
REAL,1
ll=80              !梁全长
hh=11                 !桁梁高
ww=8                  !节间长
nn1=nint(ll/ww)+1           !一侧主桁下弦纵向节点最大编号
!
!以下完成单侧平面桁架单元的建立
n,1,0,0,0              !下弦第一点
n,nn1,ll,0,0            !下弦最后一点
fill,1,nn1
n,nn1+1,ww,hh,0           !上弦第一点
n,2*nn1-2,ll-ww,hh,0
fill,nn1+1,2*nn1-2

*DO,ii,1,nn1-1,1          !下弦单元
  e,ii,ii+1
*enddo
*do,ii,nn1+1,2*nn1-3,1      !上弦单元
  e,ii,ii+1                 
*enddo

!腹杆单元（斜杆）

*do,ii,1,nn1-1,2            !奇数点
  e,ii,nn1+ii
  e,ii+2,nn1+ii
*enddo

!腹杆单元（竖杆）

*do,ii,2,nn1-1,1            
  e,ii,nn1+ii-1
*enddo


/solu
                 !约束，可根据实际情况增加约束
D,1,UY
d,1,ux
d,nn1,uy


!施加荷载
!加载

x0=0                           !从0开始加载
x1=80
LHY=0                  !加载平面的竖向坐标
D_l=8                         !单位荷载移动步长
nHzb=nint((x1-x0)/D_l+0.5)+1
*DIM,hzwz,ARRAY,nHzb          !定义荷载位置数组

xx=x0                         !单位荷载移动位置

*do,ii,1,nHzb,1
!  TIME,(ii)*Delt_Time          !结束时间
  KBC,0                       !台阶加载
  NSUBST,5                    !子步数
  fdele,all,all
      *if,xx,gt,x1,then
        *exit
      *endif    
  nsel,s,node,,node(xx,LHY,0) 
  esln
  *get,emin,elem,,num,min
  ee=emin        
  edy=0                     !先给荷载单元赋值 
  ecount=elmiqr(0,13)      
  *do,nn,1,ecount
      ye1= ny(nelem(ee,1))    !单元节点坐标  
      ye2= ny(nelem(ee,2))
      xe1= nx(nelem(ee,1))
      xe2= nx(nelem(ee,2))
      *if,abs(ye1-ye2),le,1e-6,and,(xx-xe1)*(xx-xe2),le,0,then
          edy=ee
      *endif
      ee=elnext(ee)                            
  *enddo
      cc=abs(nx(nelem(edy,1))-nx(nelem(edy,2)))          !单元x方向长
      aa=abs(nx(nelem(edy,1))-xx)                       !I节点距荷载长度
      bb=abs(nx(nelem(edy,2))-xx)                       !J节点距荷载长度
      hzi=1*bb/cc                           !I节点等效荷载
      hzj=1*aa/cc                           !j节点等效荷载

      allsel,all,all
      f,nelem(edy,1),fy,-hzi
      f,nelem(edy,2),fy,-hzj
  
  hzwz(ii)=xx                    !将荷载的实际位置存入数组
  solve
  xx=xx+D_l 
*enddo

*CFOPEN,hzwz,txt
*do,kk,1,ii,1
zzz=hzwz(kk)
*cfwrite,, zzz
*enddo
*cfclos


/POST26 

ESOL,2,10,10,smisc,1,E0E1 
ESOL,3,28,11,smisc,1,A1E0
ESOL,4,37,20,smisc,1,A1E1 
ESOL,5,27,20,smisc,1,A1E2 
ESOL,6,25,18,smisc,1,A3E4 
ESOL,7,24,7,smisc,1,A5E4   
ESOL,8,15,16,smisc,1,A4A5   
ESOL,9,5,5,smisc,1,E5E4' 
